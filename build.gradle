plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.apache.httpcomponents:httpclient:4.5.14"
    api "com.google.code.gson:gson:2.10.1"
    api "io.jsonwebtoken:jjwt-impl:0.11.5"
    api("io.jsonwebtoken:jjwt-gson:0.12.2") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api "org.slf4j:slf4j-api:2.0.9"
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = project.maven_version.endsWith("SNAPSHOT") ? "https://maven.lenni0451.net/snapshots" : "https://maven.lenni0451.net/releases"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "MinecraftAuth"
                description = "Simple and easy to use Minecraft microsoft authentication library (Java and Bedrock)"
                url = "https://github.com/RaphiMC/MinecraftAuth"
                licenses {
                    license {
                        name = "LGPL-3.0 License"
                        url = "https://github.com/RaphiMC/MinecraftAuth/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/RaphiMC/MinecraftAuth.git"
                    developerConnection = "scm:git:ssh://github.com/RaphiMC/MinecraftAuth.git"
                    url = "https://github.com/RaphiMC/MinecraftAuth.git"
                }
            }
        }
    }
}

signing {
    setRequired(false)
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
